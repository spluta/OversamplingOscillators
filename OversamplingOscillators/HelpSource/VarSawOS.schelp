TITLE:: VarSawOS
summary:: Oversampled Variable Sawtooth Oscillator
categories:: UGens>Oscillator
related:: SawBL, SquareBL, TriBL, ImpulseBL, SinOscOS, TriOS, SawOS, SquareOS, VarSawOS, PMOscOS, PM7OS, FM7OS

DESCRIPTION::
An oversampled variable saw oscillator using Jatin Chowdhurry's oversampling library from ChowDSP. Allows for wave shapes between saw up, triangle, saw down, and everything in between.


CLASSMETHODS::

METHOD:: ar

ARGUMENT:: freq
frequency - FM is possible

ARGUMENT:: phase
values are between 0 and 2 or -1 and 1
phase - PM is possible

ARGUMENT:: width
width of the VarSaw. values are between 0 and 1 - 0.5 is a triangle wave. fully modulate-able with .ar signals.

ARGUMENT:: oversample
OverSampling Index

0 = none, 1 = 2x, 2 = 4x, 3 = 8x, 4 = 16x

ARGUMENT:: mul
mul

ARGUMENT:: add
add


EXAMPLES::

code::
{VarSawOS.ar(MouseX.kr(-1000,10000), 0, 0.5, 1, 0.1)}.scope; //not bad

{VarSawOS.ar(MouseX.kr(-1000,10000), 0, 0.05, 1, 0.1)}.scope; //not bad

{VarSawOS.ar(MouseX.kr(-1000,10000), 0, 0.05, 4, 0.1)}.scope; //real nice

//phase offset works
{VarSawOS.ar(400, 0)}.plot;
{VarSawOS.ar(400, -0.5)}.plot;
{VarSawOS.ar(400, 1)}.plot;

//phase modulation works
{VarSawOS.ar(0, LFSaw.ar(400))}.plot;

//really great for interesting waves
{VarSawOS.ar(200, SinOsc.ar(400)*Line.ar(0,2,10), 0.5, 2, 0.1)}.scope;

//modulate the width
{VarSawOS.ar(200, 0, SinOsc.ar(1).range(0.1,0.9), 2, 0.1)}.scope;

//Frequency modulation is possible
({VarSawOS.ar(K2A.ar(MouseX.kr(200,4000))+
	(LFTri.ar(K2A.ar(MouseY.kr(200,4000)))*3000), 0, SinOsc.ar(1).range(0.1,0.9), 4, 0.2).dup
}.play)

//modulate everything
({VarSawOS.ar(K2A.ar(MouseX.kr(200,4000))+
	(LFTri.ar(K2A.ar(MouseY.kr(200,4000)))*3000), SinOsc.ar(400)*SinOsc.ar(0.2), SinOsc.ar(1).range(0.1,0.9), 4, 0.2).dup
}.play)


//Extreme Modulation is possible.

({
	var locIn=LocalIn.ar(1);
	var feedbackMul = 5000;
	var mod = feedbackMul*VarSawOS.ar((MouseY.kr(0, 8000)+(locIn*2000)));

	var car = VarSawOS.ar(
		(MouseX.kr(1, 8000)
			+(mod)), 0, 0.1,4);
	LocalOut.ar(car);
	car.dup*0.2
}.scope)
::
